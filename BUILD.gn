import("//build/buildflag_header.gni")
import("//build/config/arm.gni")
import("//build/config/c++/c++.gni")
import("//build/config/chromecast_build.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/dcheck_always_on.gni")
import("//build/config/logging.gni")
import("//build/config/nacl/config.gni")
import("//build/config/profiling/profiling.gni")
import("//build/config/sysroot.gni")
import("//build/config/ui.gni")
import("//build/nocompile.gni")
import("//build/timestamp.gni")
import("//build_overrides/build.gni")
import("//opencamera.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("//testing/test.gni")

component("base") {
  visibility = [ "*" ]

  sources = [ "null.cc" ]
  deps = [
    ":count_down_latch",
    ":ignore_wundef",
    ":logging",
    ":macromagic",
    ":task_util",
    ":thread",
    ":utils",
  ]

  if (avp_include_test) {
    deps += [
      ":logging_test",
      ":task_test",
      ":thread_test",
    ]
  }
}

source_set("macromagic") {
  visibility = [ "*" ]
  sources = [
    "constructor_magic.h",
    "thread_annotation.h",
  ]
}

source_set("byte_utils") {
  sources = [
    "byte_utils.cc",
    "byte_utils.h",
  ]
}

static_library("utils") {
  sources = [
    "checks.h",
    "errors.cc",
    "errors.h",
    "hexdump.cc",
    "hexdump.h",
    "types.h",
    "unique_fd.h",
    "utils.cc",
    "utils.h",
  ]
}

static_library("count_down_latch") {
  visibility = [ "*" ]
  sources = [
    "count_down_latch.cc",
    "count_down_latch.h",
  ]
}

source_set("ignore_wundef") {
  sources = [ "ignore_wundef.h" ]
}

static_library("logging") {
  sources = [
    "logging.cc",
    "logging.h",
  ]
}

source_set("thread") {
  sources = [
    "thread.cc",
    "thread.h",
    "thread_defs.h",
  ]
  deps = [
    ":count_down_latch",
    ":logging",
    ":macromagic",
  ]
}

static_library("task_util") {
  sources = [
    "task_util/default_task_runner_factory.h",
    "task_util/task.h",
    "task_util/task_runner.cc",
    "task_util/task_runner.h",
    "task_util/task_runner_base.h",
    "task_util/task_runner_factory.h",
    "task_util/to_task.h",
  ]

  sources += [
    "task_util/default_task_runner_factory_stdlib.cc",
    "task_util/task_runner_stdlib.cc",
    "task_util/task_runner_stdlib.h",
  ]
  deps = [
    ":logging",
    ":thread",
  ]
}

executable("logging_test") {
  visibility = [ "*" ]
  sources = [ "logging_test.cc" ]

  deps = [ ":logging" ]
}

executable("thread_test") {
  sources = [ "thread_test.cc" ]
  deps = [
    ":logging",
    ":thread",
  ]
}

source_set("task_runner_unittest") {
  testonly = true
  sources = [
    "test/task_runner_unittest.cc",
    "test/task_runner_unittest.h",
  ]
  deps = [
    ":logging",
    ":task_util",
    "//test:test_support",
  ]
}

executable("task_test") {
  sources = [ "task_util/task_runner_test.cc" ]
  deps = [
    ":logging",
    ":task_util",
  ]
}

executable("base_unittests") {
  testonly = true
  deps = [
    ":task_runner_unittest",
    ":task_util",
    "//test:test_main",
    "//test:test_support",
  ]
}
