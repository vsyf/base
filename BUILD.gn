import("//build/buildflag_header.gni")
import("//build/config/arm.gni")
import("//build/config/c++/c++.gni")
import("//build/config/chromecast_build.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/dcheck_always_on.gni")
import("//build/config/logging.gni")
import("//build/config/nacl/config.gni")
import("//build/config/profiling/profiling.gni")
import("//build/config/sysroot.gni")
import("//build/config/ui.gni")
import("//build/nocompile.gni")
import("//build/timestamp.gni")
import("//build_overrides/build.gni")
import("//opencamera.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("//testing/test.gni")

oc_library("base") {
  visibility = [ "*" ]

  sources = [
    "array_view.h",
    "buffer.h",
    "null.cc",
    "zero_memory.cc",
    "zero_memory.h",
  ]
  deps = [
    ":attributes",
    ":count_down_latch",
    ":ignore_wundef",
    ":logging",
    ":macromagic",
    ":task_util",
    ":thread",
    ":timeutils",
    ":type_traits",
    ":utils",
  ]

  deps += [ "third_party/sigslot" ]

  if (ave_include_test) {
    deps += [
      ":logging_test",
      ":task_test",
      ":thread_test",
    ]
  }
}

oc_library("timeutils") {
  visibility = [ "*" ]
  sources = [
    "system_time.cc",
    "system_time.h",
    "time_utils.cc",
    "time_utils.h",
  ]
  deps = [ ":utils" ]
}

oc_source_set("type_traits") {
  sources = [ "type_traits.h" ]
}

source_set("macromagic") {
  visibility = [ "*" ]
  sources = [
    "constructor_magic.h",
    "thread_annotation.h",
  ]
}

source_set("byte_utils") {
  sources = [
    "byte_utils.cc",
    "byte_utils.h",
  ]
}

source_set("checks") {
  sources = [ "checks.h" ]
}

static_library("utils") {
  sources = [
    "errors.cc",
    "errors.h",
    "hexdump.cc",
    "hexdump.h",
    "types.h",
    "unique_fd.h",
    "utils.cc",
    "utils.h",
  ]
  deps = [ ":checks" ]
}

static_library("count_down_latch") {
  visibility = [ "*" ]
  sources = [
    "count_down_latch.cc",
    "count_down_latch.h",
  ]
}

source_set("ignore_wundef") {
  sources = [ "ignore_wundef.h" ]
}

source_set("attributes") {
  sources = [ "attributes.h" ]
}

static_library("logging") {
  sources = [
    "logging.cc",
    "logging.h",
  ]
}

source_set("thread") {
  sources = [
    "thread.cc",
    "thread.h",
    "thread_defs.h",
  ]
  deps = [
    ":count_down_latch",
    ":logging",
    ":macromagic",
  ]
}

static_library("task_util") {
  sources = [
    "task_util/default_task_runner_factory.h",
    "task_util/pending_task_flag.cc",
    "task_util/pending_task_flag.h",
    "task_util/repeating_task.cc",
    "task_util/repeating_task.h",
    "task_util/task.h",
    "task_util/task_runner.cc",
    "task_util/task_runner.h",
    "task_util/task_runner_base.cc",
    "task_util/task_runner_base.h",
    "task_util/task_runner_factory.h",
    "task_util/to_task.h",
  ]

  sources += [
    "task_util/default_task_runner_factory_stdlib.cc",
    "task_util/task_runner_stdlib.cc",
    "task_util/task_runner_stdlib.h",
  ]

  configs += [ "//build/config/compiler:rtti" ]

  deps = [
    ":logging",
    ":thread",
  ]
}

static_library("sequence_checker") {
  sources = [
    "sequence_checker.h",
    "sequence_checker_internal.cc",
    "sequence_checker_internal.h",
  ]
  deps = [
    ":logging",
    ":task_util",
  ]
}

static_library("numerics") {
  sources = [
    "numerics/exp_filter.cc",
    "numerics/exp_filter.h",
  ]
}

oc_source_set("safe_conversions") {
  sources = [
    "numerics/safe_conversions.h",
    "numerics/safe_conversions_impl.h",
  ]
  deps = [ ":checks" ]
}

oc_source_set("safe_compare") {
  sources = [
    "numerics/safe_compare.h",
    "numerics/safe_compare_impl.h",
  ]
  deps = [ ":type_traits" ]
}

oc_source_set("safe_minmax") {
  sources = [ "numerics/safe_minmax.h" ]
  deps = [
    ":checks",
    ":safe_compare",
    ":type_traits",
  ]
}

oc_source_set("divide_round") {
  sources = [ "numerics/divide_round.h" ]
  deps = [
    ":checks",
    ":safe_compare",
  ]
}

#--------------------

#oc_source_set("unit_base") {
#  sources = [ "units/unit_base.h" ]
#  deps = [
#    ":checks",
#    ":divide_round",
#    ":safe_conversions",
#  ]
#}

#--------------------

executable("logging_test") {
  visibility = [ "*" ]
  sources = [ "logging_test.cc" ]

  deps = [ ":logging" ]
}

executable("thread_test") {
  sources = [ "thread_test.cc" ]
  deps = [
    ":logging",
    ":thread",
  ]
}

source_set("task_runner_unittest") {
  testonly = true
  sources = [
    "test/repeating_task_unittest.cc",
    "test/task_runner_for_test.cc",
    "test/task_runner_for_test.h",
    "test/task_runner_unittest.cc",
    "test/task_runner_unittest.h",
  ]
  deps = [
    ":logging",
    ":task_util",
    "//test:test_support",
  ]
}

executable("task_test") {
  sources = [ "task_util/task_runner_test.cc" ]
  deps = [
    ":logging",
    ":task_util",
  ]
}

source_set("numerics_unittest") {
  testonly = true
  sources = [ "numerics/exp_filter_unittest.cc" ]
  deps = [
    ":logging",
    ":numerics",
    "//test:test_support",
  ]
}

executable("base_unittests") {
  testonly = true
  deps = [
    ":numerics_unittest",
    ":task_runner_unittest",
    ":task_util",
    "units:units_unittests_source",
    "//test:test_main",
    "//test:test_support",
  ]
}
